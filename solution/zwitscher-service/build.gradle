buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'idea'

version = '1.1.1'

jar {
    baseName = 'zwitscher-service'
    version = "$version"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'org.springframework.cloud:spring-cloud-starter-hystrix'

    compile 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    compile 'org.springframework.cloud:spring-cloud-starter-consul-config'

    compile 'org.springframework.boot:spring-boot-starter-social-twitter'
    compile 'org.springframework.boot:spring-boot-starter-web'

    compile 'io.prometheus:simpleclient:0.0.19'
    compile 'io.prometheus:simpleclient_common:0.0.19'
    compile 'io.prometheus:simpleclient_spring_boot:0.0.19'
    compile 'io.prometheus:simpleclient_servlet:0.0.19'

    compile 'io.fabric8:spring-cloud-starter-kubernetes:0.1.4'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR1"
    }
}

springBoot {
    executable = true
}

docker {
    url = System.env.DOCKER_HOST?.replace('tcp', 'https') ?: 'https://192.168.99.100:2376'
    certPath = file("${System.getProperty('user.home')}/.docker/machine/machines/default")
}

import com.bmuschko.gradle.docker.tasks.DockerInfo
import com.bmuschko.gradle.docker.tasks.DockerVersion
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage

task dockerInfo(type: DockerInfo, description: 'Display Docker info') {}
task dockerVersion(type: DockerVersion, description: 'Display Docker version') {}

task buildDockerImage(type: DockerBuildImage, description: 'Build Docker image') {
    inputDir = projectDir
    noCache = false
    remove = true
    tag = "$project.name:$version-${System.env['USER']}"
}

task removeDockerImage(type: DockerRemoveImage, description: 'Remove Docker image') {
    imageId = "$project.name:$version-${System.env['USER']}"
}

task createConsul(type: Exec, group: 'application', description: 'Create Consul container') {
    commandLine 'docker', 'run', '-d', '--name', 'zwitscher-consul', '-p', '8400:8400', '-p', '8500:8500', 'consul'
}

task startConsul(type: Exec, group: 'application', description: 'Start Consul container') {
    commandLine 'docker', 'start', 'zwitscher-consul'
}

task stopConsul(type: Exec, group: 'application', description: 'Stop Consul container') {
    commandLine 'docker', 'stop', 'zwitscher-consul'
}
